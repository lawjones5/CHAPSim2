module io_restart_mod
  use decomp_2d_io
  use io_files_mod
  use io_tools_mod
  use parameters_constant_mod
  use print_msg_mod
  use udf_type_mod
  implicit none 

  character(len=10), parameter :: io_name = "restart-io"

  public  :: write_instantaneous_flow
  public  :: read_instantaneous_flow
  public  :: restore_flow_variables_from_restart

  public  :: write_instantaneous_thermo
  public  :: read_instantaneous_thermo
  public  :: restore_thermo_variables_from_restart

  private :: append_instantaneous_xoutlet
  private :: assign_instantaneous_xinlet
  public  :: write_instantaneous_xoutlet
  public  :: read_instantaneous_xinlet

  !private :: write_instantaneous_plane !not used
  !private :: read_instantaneous_plane !not used

contains 

!==========================================================================================================
!==========================================================================================================
  subroutine write_instantaneous_flow(fl, dm)
    use io_tools_mod
    implicit none
    type(t_domain), intent(in) :: dm
    type(t_flow),   intent(in) :: fl

    character(64):: data_flname_path
    character(64):: keyword

    if(nrank == 0) call Print_debug_inline_msg("writing out instantaneous 3d flow data ...")

    call write_one_3d_array(fl%qx, 'qx', dm%idom, fl%iteration, dm%dpcc, dm%io_mode)
    call write_one_3d_array(fl%qy, 'qy', dm%idom, fl%iteration, dm%dcpc, dm%io_mode)
    call write_one_3d_array(fl%qz, 'qz', dm%idom, fl%iteration, dm%dccp, dm%io_mode)
    call write_one_3d_array(fl%pres, 'pr', dm%idom, fl%iteration, dm%dccc, dm%io_mode)

    if(nrank == 0) call Print_debug_end_msg()
    return
  end subroutine
!==========================================================================================================
!==========================================================================================================
  subroutine write_instantaneous_thermo(tm, dm)
    use thermo_info_mod
    implicit none
    type(t_domain), intent(in) :: dm
    type(t_thermo), intent(in) :: tm

    character(64):: data_flname_path
    character(64):: keyword
    

    if(nrank == 0) call Print_debug_inline_msg("writing out instantaneous 3d thermo data ...")

    call write_one_3d_array(tm%rhoh,  'rhoh', dm%idom, tm%iteration, dm%dccc, dm%io_mode)
    call write_one_3d_array(tm%tTemp, 'temp', dm%idom, tm%iteration, dm%dccc, dm%io_mode)

    if(nrank == 0) call Print_debug_end_msg()
    return
  end subroutine
!==========================================================================================================
!==========================================================================================================
  subroutine read_instantaneous_flow(fl, dm)
    use io_tools_mod
    implicit none
    type(t_domain), intent(inout) :: dm
    type(t_flow),   intent(inout) :: fl

    character(64):: data_flname
    character(64):: keyword


    if(nrank == 0) call Print_debug_inline_msg("read instantaneous flow data ...")
    fl%iteration = fl%iterfrom
    fl%time = real(fl%iterfrom, WP) * dm%dt 

    call read_one_3d_array(fl%qx,   'qx', dm%idom, fl%iterfrom, dm%dpcc)
    call read_one_3d_array(fl%qy,   'qy', dm%idom, fl%iterfrom, dm%dcpc)
    call read_one_3d_array(fl%qz,   'qz', dm%idom, fl%iterfrom, dm%dccp)
    call read_one_3d_array(fl%pres, 'pr', dm%idom, fl%iterfrom, dm%dccc)
    
    if(nrank == 0) call Print_debug_end_msg()
    return
  end subroutine

!==========================================================================================================
!==========================================================================================================
  subroutine restore_flow_variables_from_restart(fl, dm)
    use boundary_conditions_mod
    use find_max_min_ave_mod
    use mpi_mod
    use solver_tools_mod
    use wtformat_mod
    implicit none
    type(t_domain), intent(in) :: dm
    type(t_flow),   intent(inout) :: fl
    real(WP) :: ubulk
    

    call Get_volumetric_average_3d(dm, dm%dpcc, fl%qx, ubulk, SPACE_AVERAGE, "ux")
    if(nrank == 0) then
        call Print_debug_inline_msg("The restarted mass flux is:")
        write (*, wrtfmt1e) ' average[u(x,y,z)]_[x,y,z]: ', ubulk
    end if
    !----------------------------------------------------------------------------------------------------------
    ! to check maximum velocity
    !----------------------------------------------------------------------------------------------------------
    call Find_max_min_3d(fl%qx, opt_name="qx: ")
    call Find_max_min_3d(fl%qy, opt_name="qy: ")
    call Find_max_min_3d(fl%qz, opt_name="qz: ")
    !----------------------------------------------------------------------------------------------------------
    ! to set up other parameters for flow only, which will be updated in thermo flow.
    !----------------------------------------------------------------------------------------------------------
    fl%pcor(:, :, :) = ZERO
    fl%pcor_zpencil_ggg(:, :, :) = ZERO

    return
  end subroutine
!==========================================================================================================
!==========================================================================================================
  subroutine read_instantaneous_thermo(tm, dm)
    use io_tools_mod
    use thermo_info_mod
    implicit none
    type(t_domain), intent(inout) :: dm
    type(t_thermo), intent(inout) :: tm

    character(64):: data_flname
    character(64):: keyword

    if (.not. dm%is_thermo) return
    if(nrank == 0) call Print_debug_inline_msg("read instantaneous thermo data ...")

    tm%iteration = tm%iterfrom
    tm%time = real(tm%iterfrom, WP) * dm%dt 

    call read_one_3d_array(tm%rhoh,  'rhoh', dm%idom, tm%iteration, dm%dccc)
    call read_one_3d_array(tm%tTemp, 'temp', dm%idom, tm%iteration, dm%dccc)


    if(nrank == 0) call Print_debug_end_msg()
    return
  end subroutine
!==========================================================================================================
  subroutine restore_thermo_variables_from_restart(fl, tm, dm)
    use convert_primary_conservative_mod
    use eq_energy_mod
    use solver_tools_mod
    use thermo_info_mod
    use udf_type_mod
    type(t_domain), intent(inout) :: dm
    type(t_flow),   intent(inout) :: fl
    type(t_thermo), intent(inout) :: tm

    if (.not. dm%is_thermo) return

    call Update_thermal_properties(fl%dDens, fl%mVisc, tm, dm)
    call convert_primary_conservative (dm, fl%dDens, IQ2G, IALL, fl%qx, fl%qy, fl%qz, fl%gx, fl%gy, fl%gz)

    fl%dDens0(:, :, :) = fl%dDens(:, :, :)

    return
  end subroutine


!==========================================================================================================
  subroutine append_instantaneous_xoutlet(fl, dm, niter)
    implicit none 
    type(t_flow), intent(in) :: fl
    type(t_domain), intent(inout) :: dm
    integer, intent(out) :: niter

    integer :: j, k
    type(DECOMP_INFO) :: dtmp

    ! based on x pencil
    if(.not. dm%is_record_xoutlet) return
    if(fl%iteration < dm%ndbstart) return

    ! if dm%ndbfre = 10, and start from 36 to 65
    ! store : 
    !     To store: 36, 37, 38,...,44, 45 at file 10*(iter=0)
    !     To store: 46, 47, 48,...,54, 55 at file 10*(iter=1)
    !     To store: 56, 57, 58,...,64, 65 at file 10*(iter=2)
    !        niter: 1,  2,  3, ..., 9, 0
    niter = mod(fl%iteration - dm%ndbstart + 1, dm%ndbfre) !
    if(niter == 1) then ! re-initialize at begin of each cycle
      dm%fbcx_qx_outl1 = MAXP
      dm%fbcx_qx_outl2 = MAXP
      dm%fbcx_qy_outl1 = MAXP
      dm%fbcx_qy_outl2 = MAXP
      dm%fbcx_qz_outl1 = MAXP
      dm%fbcx_qz_outl2 = MAXP
      dm%fbcx_pr_outl1 = MAXP
      dm%fbcx_pr_outl2 = MAXP
    else if(niter == 0) then
      niter =  dm%ndbfre
    else
      ! do nothing
    end if

    dtmp = dm%dpcc
    do j = 1, dtmp%xsz(2)
      do k = 1, dtmp%xsz(3)
        dm%fbcx_qx_outl1(niter, j, k) = fl%qx(dtmp%xsz(1),   j, k)
        dm%fbcx_qx_outl2(niter, j, k) = fl%qx(dtmp%xsz(1)-1, j, k)
      end do
    end do

    !write(*, *) 'j, fl%qx(1, j, 1), dm%fbcx_qx_outl1(niter, j, 1)'
    ! do j = 1, dm%dpcc%xsz(2)
    !   write(*, *) j, fl%qx(dtmp%xsz(1), j, 1), dm%fbcx_qx_outl1(niter, j, 1)
    ! end do

    dtmp = dm%dcpc
    do j = 1, dtmp%xsz(2)
      do k = 1, dtmp%xsz(3)
        dm%fbcx_qy_outl1(niter, j, k) = fl%qy(dtmp%xsz(1),   j, k)
        dm%fbcx_qy_outl2(niter, j, k) = fl%qy(dtmp%xsz(1)-1, j, k)
      end do
    end do

    dtmp = dm%dccp
    do j = 1, dtmp%xsz(2)
      do k = 1, dtmp%xsz(3)
        dm%fbcx_qz_outl1(niter, j, k) = fl%qz(dtmp%xsz(1),   j, k)
        dm%fbcx_qz_outl2(niter, j, k) = fl%qz(dtmp%xsz(1)-1, j, k)
      end do
    end do

    dtmp = dm%dccc
    do j = 1, dtmp%xsz(2)
      do k = 1, dtmp%xsz(3)
        dm%fbcx_pr_outl1(niter, j, k) = fl%pres(dtmp%xsz(1),   j, k)
        dm%fbcx_pr_outl2(niter, j, k) = fl%pres(dtmp%xsz(1)-1, j, k)
      end do
    end do

    return
  end subroutine
! !==========================================================================================================
!   subroutine write_instantaneous_plane(var, keyword, idom, iter, niter, dtmp)
!     implicit none 
!     real(WP), contiguous, intent(in) :: var( :, :, :)
!     type(DECOMP_INFO), intent(in) :: dtmp
!     character(*), intent(in) :: keyword
!     integer, intent(in) :: idom
!     integer, intent(in) :: iter, niter

!     character(64):: data_flname_path

!     call generate_pathfile_name(data_flname_path, idom, trim(keyword), dir_data, 'bin', iter)

!     if(nrank==0) write(*, *) 'Write outlet plane data to ['//trim(data_flname_path)//"]"
 
!     !call decomp_2d_open_io (io_in2outlet, trim(data_flname_path), decomp_2d_write_mode)
!     !call decomp_2d_start_io(io_in2outlet, trim(data_flname_path))!

!     !call decomp_2d_write_outflow(trim(data_flname_path), trim(keyword), niter, var, io_in2outlet, dtmp)
!     !call decomp_2d_write_plane(IPENCIL(1), var, 1, dtmp%xsz(1), trim(data_flname_path), dtmp)
!     call decomp_2d_write_plane(IPENCIL(1), var, data_flname_path, &
!                                 opt_nplanes=niter, &
!                                 opt_decomp = dtmp)
!     !call decomp_2d_end_io(io_in2outlet, trim(data_flname_path))
!     !call decomp_2d_close_io(io_in2outlet, trim(data_flname_path))

!     return
!   end subroutine
!==========================================================================================================
  subroutine write_instantaneous_xoutlet(fl, dm)
    use io_tools_mod
    implicit none 
    type(t_flow), intent(in) :: fl
    type(t_domain), intent(inout) :: dm
    
    character(64):: data_flname_path
    integer :: idom, niter, iter, j

    if(.not. dm%is_record_xoutlet) return
    if(fl%iteration < dm%ndbstart) return

    call append_instantaneous_xoutlet(fl, dm, niter)

    ! if dm%ndbfre = 10, and start from 36 to 65
    ! store : 
    !     To store: 36, 37, 38,...,44, 45 at file 10*(iter=0)
    !     To store: 46, 47, 48,...,54, 55 at file 10*(iter=1)
    !     To store: 56, 57, 58,...,64, 65 at file 10*(iter=2)
    !        niter: 1,  2,  3, ..., 9, 0
    !write(*,*) 'iter, niter', fl%iteration, niter
    if(niter == dm%ndbfre) then
      if( mod(fl%iteration - dm%ndbstart + 1, dm%ndbfre) /= 0 .and. nrank == 0) &
      call Print_warning_msg("niter /= dm%ndbfre, something wrong in writing outlet data")
      iter = (fl%iteration - dm%ndbstart)/dm%ndbfre * dm%ndbfre
      call write_one_3d_array(dm%fbcx_qx_outl1, 'outlet1_qx', dm%idom, iter, dm%dxcc, dm%io_mode)
      call write_one_3d_array(dm%fbcx_qx_outl2, 'outlet2_qx', dm%idom, iter, dm%dxcc, dm%io_mode)
      call write_one_3d_array(dm%fbcx_qy_outl1, 'outlet1_qy', dm%idom, iter, dm%dxpc, dm%io_mode)
      call write_one_3d_array(dm%fbcx_qy_outl2, 'outlet2_qy', dm%idom, iter, dm%dxpc, dm%io_mode)
      call write_one_3d_array(dm%fbcx_qz_outl1, 'outlet1_qz', dm%idom, iter, dm%dxcp, dm%io_mode)
      call write_one_3d_array(dm%fbcx_qz_outl2, 'outlet2_qz', dm%idom, iter, dm%dxcp, dm%io_mode)
      call write_one_3d_array(dm%fbcx_pr_outl1, 'outlet1_pr', dm%idom, iter, dm%dxcc, dm%io_mode)
      call write_one_3d_array(dm%fbcx_pr_outl2, 'outlet2_pr', dm%idom, iter, dm%dxcc, dm%io_mode)
      !if(nrank == 0) write (*,*) " writing outlet database at ", fl%iteration, 'for iter =', iter -  dm%ndbfre, 'to ', iter
    end if
! #ifdef DEBUG_STEPS
!     write(*,*) 'outlet bc'
!     do j = 1, dm%dpcc%xsz(2)
!       write(*,*) dm%dpcc%xst(2) + j - 1, &
!       dm%fbcx_qx_outl1(niter, j, 1), dm%fbcx_qx_outl2(niter, j, 1)
!     end do
!     write(*,*) 'inlet bc'
!     do j = 1, dm%dpcc%xsz(2)
!       write(*,*) dm%dpcc%xst(2) + j - 1, &
!       dm%fbcx_qx_out1(niter, j, 1), dm%fbcx_qx_out2(niter, j, 1)
!     end do
! #endif
    return
  end subroutine
!==========================================================================================================
  subroutine assign_instantaneous_xinlet(fl, dm)
    use convert_primary_conservative_mod
    use typeconvert_mod
    implicit none 
    type(t_flow), intent(inout) :: fl
    type(t_domain), intent(inout) :: dm

    integer :: iter, j, k
    type(DECOMP_INFO) :: dtmp

    ! based on x pencil
    if(.not. dm%is_read_xinlet) return

    iter = max(1, fl%iteration)
    iter = mod(iter-1, dm%ndbfre) + 1

    !if (nrank == 0) &
    !  call Print_debug_mid_msg('inlet assigned at iteration '//trim(int2str(iter))

    if(dm%ibcx_nominal(1, 1) == IBC_DATABASE) then
      dtmp = dm%dpcc
      do j = 1, dtmp%xsz(2)
        do k = 1, dtmp%xsz(3)
          dm%fbcx_qx(1, j, k) = dm%fbcx_qx_inl1(iter, j, k)
          dm%fbcx_qx(3, j, k) = dm%fbcx_qx_inl2(iter, j, k)
          ! check, below 
          !fl%qx(1, j, k) = dm%fbcx_qx(1, j, k)
        end do
      end do
      !if(nrank == 0) write(*,*) 'fbcx_in1 = ', iter, dm%fbcx_qx_inl1(iter, :, 1)
      !if(nrank == 0) write(*,*) 'fbcx_in2 = ', iter, dm%fbcx_qx_inl1(iter, :, 32)
      !if(nrank == 0) write(*,*) 'fbcx_qx1 = ', iter, dm%fbcx_qx(1, :, 1)
      !if(nrank == 0) write(*,*) 'fbcx_qx2 = ', iter, dm%fbcx_qx(1, :, 32)
    end if


    if(dm%ibcx_nominal(1, 2) == IBC_DATABASE) then
      dtmp = dm%dcpc
      do j = 1, dtmp%xsz(2)
        do k = 1, dtmp%xsz(3)
          dm%fbcx_qy(1, j, k) = dm%fbcx_qy_inl1(iter, j, k)
          dm%fbcx_qy(3, j, k) = dm%fbcx_qy_inl2(iter, j, k)
        end do
      end do
      !if(nrank == 0) write(*,*) 'fbcx_qy = ', iter, dm%fbcx_qy(1, :, :)
    end if

    if(dm%ibcx_nominal(1, 3) == IBC_DATABASE) then
      dtmp = dm%dccp
      do j = 1, dtmp%xsz(2)
        do k = 1, dtmp%xsz(3)
          dm%fbcx_qz(1, j, k) = dm%fbcx_qz_inl1(iter, j, k)
          dm%fbcx_qz(3, j, k) = dm%fbcx_qz_inl2(iter, j, k)
        end do
      end do
      !if(nrank == 0) write(*,*) 'fbcx_qz = ', iter, dm%fbcx_qz(1, :, :)
    end if

    if(dm%ibcx_nominal(1, 4) == IBC_DATABASE) then
      dtmp = dm%dccc
      do j = 1, dtmp%xsz(2)
        do k = 1, dtmp%xsz(3)
          dm%fbcx_pr(1, j, k) = dm%fbcx_pr_inl1(iter, j, k)
          dm%fbcx_pr(3, j, k) = dm%fbcx_pr_inl2(iter, j, k)
        end do
      end do
      !if(nrank == 0) write(*,*) 'fbcx_pr = ', iter, dm%fbcx_pr(1, :, :)
    end if

    if(dm%is_thermo) then
      call convert_primary_conservative(dm, fl%dDens, IQ2G, IBND)
    end if

    return
  end subroutine
! !==========================================================================================================
!   subroutine read_instantaneous_plane(var, keyword, idom, iter, nfre, dtmp)
!     use decomp_2d_io
!     implicit none 
!     real(WP), contiguous, intent(out) :: var( :, :, :)
!     type(DECOMP_INFO), intent(in) :: dtmp
!     character(*), intent(in) :: keyword
!     integer, intent(in) :: idom
!     integer, intent(in) :: iter
!     integer, intent(in) :: nfre

!     character(64):: data_flname_path, flname

!     call generate_pathfile_name(data_flname_path, idom, trim(keyword), dir_data, 'bin', iter, flname)

!     !call decomp_2d_open_io (io_in2outlet, trim(data_flname_path), decomp_2d_read_mode)
!     if(nrank == 0) call Print_debug_inline_msg("Read data on a plane from file: "//trim(data_flname_path))
!     !call decomp_2d_read_inflow(trim(data_flname_path), trim(keyword), nfre, var, io_in2outlet, dtmp)
!     call decomp_2d_read_plane(IPENCIL(1), var, data_flname_path, nfre, &
!                                 opt_decomp = dtmp)

!     !decomp_2d_read_plane(ipencil, var, varname, nplanes, &
!                               !  opt_dirname, &
!                               !  opt_mpi_file_open_info, &
!                               !  opt_mpi_file_set_view_info, &
!                               !  opt_reduce_prec, &
!                               !  opt_decomp, &
!                               !  opt_nb_req, &
!                               !  opt_io)
!     !write(*,*) var
!     !call decomp_2d_close_io(io_in2outlet, trim(data_flname_path))

!     return
!   end subroutine
!==========================================================================================================
  subroutine read_instantaneous_xinlet(fl, dm, opt_iter)
    use io_tools_mod
    use typeconvert_mod
    implicit none 
    type(t_flow), intent(inout) :: fl
    type(t_domain), intent(inout) :: dm
    integer, intent(in), optional :: opt_iter

    character(64):: data_flname_path
    integer :: iter, niter, nblock, nblocks


    if(.not. dm%is_read_xinlet) return
    ! ----------------------------------------------------------------------------
    ! if dm%ndbfre = 10, and start from 36 to 65
    ! store : 
    !     To store: 36, 37, 38,...,44, 45 at file 10*1 at block = 1
    !     To store: 46, 47, 48,...,54, 55 at file 10*2 at block = 2
    !     To store: 56, 57, 58,...,64, 65 at file 10*3 at block = 3
    !        niter: 1,  2,  3, ..., 9, 0
    ! read: 
    !     iter = 1, 2, 3, ...10, read file 10*1 at block = 1
    !     iter = 11, 12, ...,20, read file 10*2 at block = 2
    !     iter = 21, 22, ...,30, read file 10*3 at block = 3
    !     iter = 31, 32, ...,40, read file 10*1 at block = 1
    ! ----------------------------------------------------------------------------
    iter = fl%iteration
    if (present(opt_iter)) iter = opt_iter
    ! ----------------------------------------------------------------------------
    ! Only read if current iteration is the first of the block
    ! ----------------------------------------------------------------------------
    if (mod(iter-1, dm%ndbfre)==0 .or. iter == (fl%iterfrom+1)) then
      nblocks = (dm%ndbend - dm%ndbstart + 1) / dm%ndbfre
      if ((dm%ndbend - dm%ndbstart + 1) > nblocks*dm%ndbfre) nblocks = nblocks + 1
      nblock = mod((iter - 1) / dm%ndbfre, nblocks)
      niter = dm%ndbfre * nblock
      if(nrank == 0) &
      call Print_debug_mid_msg('Read inlet database at iteration '//trim(int2str(iter))&
        //' mapped to file name ='//trim(int2str(niter)))
      call read_one_3d_array(dm%fbcx_qx_inl1, 'outlet1_qx', dm%idom, niter, dm%dxcc)
      call read_one_3d_array(dm%fbcx_qx_inl2, 'outlet2_qx', dm%idom, niter, dm%dxcc)
      call read_one_3d_array(dm%fbcx_qy_inl1, 'outlet1_qy', dm%idom, niter, dm%dxpc)
      call read_one_3d_array(dm%fbcx_qy_inl2, 'outlet2_qy', dm%idom, niter, dm%dxpc)
      call read_one_3d_array(dm%fbcx_qz_inl1, 'outlet1_qz', dm%idom, niter, dm%dxcp)
      call read_one_3d_array(dm%fbcx_qz_inl2, 'outlet2_qz', dm%idom, niter, dm%dxcp)
      !call read_one_3d_array(dm%fbcx_pr_inl1, 'outlet1_pr', dm%idom, niter, dm%dxcc)
      !call read_one_3d_array(dm%fbcx_pr_inl2, 'outlet2_pr', dm%idom, niter, dm%dxcc)
    end if

    ! ----------------------------------------------------------------------------
    ! Assign inlet data for every iteration (after reading block)
    ! ----------------------------------------------------------------------------
    call assign_instantaneous_xinlet(fl, dm)

    return
  end subroutine
end module 
!==========================================================================================================
!==========================================================================================================
module io_field_interpolation_mod
  USE precision_mod
  use udf_type_mod
  implicit none

  type(t_domain) :: domain_tgt
  type(t_flow)   :: flow_tgt
  type(t_thermo) :: thermo_tgt
  character(len = 21) :: input_tgt = 'input_chapsim_tgt.ini'

  integer, parameter :: XLOC_CELL = 1, &
                        XLOC_FACE = 2, &
                        YLOC_CELL = 3, &
                        YLOC_FACE = 4, &
                        ZLOC_CELL = 5, &
                        ZLOC_FACE = 6

  private :: Read_input_parameters_tgt
  private :: binary_search_loc2index
  private :: trilinear_interp_point
  private :: setup_extension_mapping
  private :: build_up_interp_target_field_flow
  public  :: output_interp_target_field

  contains 
!==========================================================================================================
  subroutine Read_input_parameters_tgt(dm, flinput)
    use parameters_constant_mod
    use print_msg_mod
    implicit none
    character(len = *), intent(in) :: flinput 
    type(t_domain), intent(inout) :: dm

    integer, parameter :: IOMSG_LEN = 200
    character(len = IOMSG_LEN) :: iotxt
    integer :: ioerr, inputUnit
    integer  :: slen
    character(len = 80) :: secname
    character(len = 80) :: varname
    ! open file1000dd
    open ( newunit = inputUnit, &
           file    = flinput, &
           status  = 'old', &
           action  = 'read', &
           iostat  = ioerr, &
           iomsg   = iotxt )
    if(ioerr /= 0) then
      ! write (*, *) 'Problem openning : ', flinput, ' for reading.'
      ! write (*, *) 'Message: ', trim (iotxt)
      call Print_error_msg('Error in opening the input file:'//trim(flinput)//' for reading. Message: '//trim(iotxt))
    end if
    !
    if(nrank == 0) &
    call Print_debug_start_msg("Reading General Parameters from "//flinput//" ...")
    ! reading input
    do 
      ! reading headings/comments
      read(inputUnit, '(a)', iostat = ioerr) secname
      slen = len_trim(secname)
      if (ioerr /=0 ) exit
      if ( (secname(1:1) == ';') .or. &
           (secname(1:1) == '#') .or. &
           (secname(1:1) == ' ') .or. &
           (slen == 0) ) then
        cycle
      end if
      if(nrank == 0) call Print_debug_mid_msg("Reading "//secname(1:slen))
      ! [domain]
      if ( secname(1:slen) == '[domain]' ) then
        read(inputUnit, *, iostat = ioerr) varname, dm%icase
        read(inputUnit, *, iostat = ioerr) varname, dm%lxx
        read(inputUnit, *, iostat = ioerr) varname, dm%lyt
        read(inputUnit, *, iostat = ioerr) varname, dm%lyb
        read(inputUnit, *, iostat = ioerr) varname, dm%lzz
      ! [mesh] 
      else if ( secname(1:slen) == '[mesh]' ) then
        read(inputUnit, *, iostat = ioerr) varname, dm%nc(1)
        read(inputUnit, *, iostat = ioerr) varname, dm%nc(2)
        read(inputUnit, *, iostat = ioerr) varname, dm%nc(3)
        read(inputUnit, *, iostat = ioerr) varname, dm%istret
        read(inputUnit, *, iostat = ioerr) varname, dm%mstret, dm%rstret
        !read(inputUnit, *, iostat = ioerr) varname, dm%ifft_lib
      else
        exit
      end if
    end do
    ! end of reading, clearing dummies
    if(.not.IS_IOSTAT_END(ioerr)) &
    call Print_error_msg( 'Problem reading '//flinput // &
    ' in Subroutine: '// "Read_general_input_tgt")

    close(inputUnit)
    return
  end subroutine 
!==========================================================================================================
  SUBROUTINE binary_search_loc2index(x_target, x_array, idx)
    IMPLICIT NONE
    REAL(WP), INTENT(IN) :: x_target
    REAL(WP), INTENT(IN) :: x_array(:)
    INTEGER, INTENT(OUT) :: idx
    !
    INTEGER :: n, left, right, mid
    !
    n = size(x_array, 1)
    ! Handle boundary cases
    IF (x_target <= x_array(1)) THEN
      idx = 1
      RETURN
    END IF
    IF (x_target >= x_array(n)) THEN
      idx = n - 1
      RETURN
    END IF
    ! Binary search
    left = 1
    right = n
    DO WHILE (right - left > 1)
      mid = (left + right) / 2
      IF (x_target <= x_array(mid)) THEN
        right = mid
      ELSE
        left = mid
      END IF
    END DO
    idx = left
    RETURN
  END SUBROUTINE binary_search_loc2index
!==========================================================================================================
  SUBROUTINE trilinear_interp_point(x_target, y_target, z_target, &
                                        x_src, y_src, z_src, var_src, &
                                        var_interp)
    USE parameters_constant_mod
    IMPLICIT NONE
    REAL(WP), INTENT(IN) :: x_target, y_target, z_target
    REAL(WP), INTENT(IN) :: x_src(:), y_src(:), z_src(:)
    REAL(WP), INTENT(IN) :: var_src(:, :, :)
    REAL(WP), INTENT(OUT) :: var_interp

    INTEGER :: i_src, j_src, k_src, nx, ny, nz
    REAL(WP) :: xi, eta, zeta
    REAL(WP) :: dx, dy, dz
    REAL(WP) :: c000, c001, c010, c011, c100, c101, c110, c111
    REAL(WP) :: c00, c01, c10, c11, c0, c1

    nx = SIZE(x_src)
    ny = SIZE(y_src)
    nz = SIZE(z_src)

    !-------------------------------------------------------------
    ! Find enclosing cell indices
    !-------------------------------------------------------------
    CALL binary_search_loc2index(x_target, x_src, i_src)
    CALL binary_search_loc2index(y_target, y_src, j_src)
    CALL binary_search_loc2index(z_target, z_src, k_src)

    ! Clamp indices to valid range (ensure i_src+1 <= nx)
    i_src = MIN(i_src, nx-1)
    j_src = MIN(j_src, ny-1)
    k_src = MIN(k_src, nz-1)

    !-------------------------------------------------------------
    ! Compute normalized coordinates within the cell
    !-------------------------------------------------------------
    dx = x_src(i_src+1) - x_src(i_src)
    dy = y_src(j_src+1) - y_src(j_src)
    dz = z_src(k_src+1) - z_src(k_src)

    ! Avoid division by zero
    IF (dabs(dx) <= MINP) dx = 1.0_wp
    IF (dabs(dy) <= MINP) dy = 1.0_wp
    IF (dabs(dz) <= MINP) dz = 1.0_wp

    xi   = (x_target - x_src(i_src)) / dx
    eta  = (y_target - y_src(j_src)) / dy
    zeta = (z_target - z_src(k_src)) / dz

    ! Clamp normalized coordinates to [0,1] to avoid extrapolation outside last cell
    xi   = MAX(0.0_wp, MIN(1.0_wp, xi))
    eta  = MAX(0.0_wp, MIN(1.0_wp, eta))
    zeta = MAX(0.0_wp, MIN(1.0_wp, zeta))

    !-------------------------------------------------------------
    ! Trilinear interpolation
    !-------------------------------------------------------------
    c000 = var_src(i_src  , j_src  , k_src  )
    c001 = var_src(i_src  , j_src  , k_src+1)
    c010 = var_src(i_src  , j_src+1, k_src  )
    c011 = var_src(i_src  , j_src+1, k_src+1)
    c100 = var_src(i_src+1, j_src  , k_src  )
    c101 = var_src(i_src+1, j_src  , k_src+1)
    c110 = var_src(i_src+1, j_src+1, k_src  )
    c111 = var_src(i_src+1, j_src+1, k_src+1)

    ! Interpolate in z
    c00 = c000*(1.0_wp - zeta) + c001*zeta
    c01 = c010*(1.0_wp - zeta) + c011*zeta
    c10 = c100*(1.0_wp - zeta) + c101*zeta
    c11 = c110*(1.0_wp - zeta) + c111*zeta

    ! Interpolate in y
    c0 = c00*(1.0_wp - eta) + c01*eta
    c1 = c10*(1.0_wp - eta) + c11*eta

    ! Interpolate in x
    var_interp = c0*(1.0_wp - xi) + c1*xi
    RETURN
  END SUBROUTINE trilinear_interp_point
!==========================================================================================================
  subroutine setup_extension_mapping(src_len, tgt_len, tgt_spacing, extend_mode, extend_length)
    use parameters_constant_mod
    implicit none

    real(WP), intent(in)  :: src_len, tgt_len, tgt_spacing
    integer , intent(out) :: extend_mode
    real(WP), intent(out) :: extend_length

    if (src_len > tgt_len) then
      extend_mode = 1
    else if (src_len < tgt_len) then
      extend_mode = 2
    else
      extend_mode = 0
    end if

    extend_length = ZERO
    if (extend_mode == 2) then
      extend_length = MAX((tgt_len - src_len) / FIVE, TWO * tgt_spacing)
    end if

    return
  end subroutine setup_extension_mapping
!==========================================================================================================
  subroutine build_up_interp_target_field_flow(fl_src, dm_src, fl_tgt, dm_tgt)
    use parameters_constant_mod
    use print_msg_mod
    use udf_type_mod
    implicit none
    !
    type(t_domain), intent(in)    :: dm_src
    type(t_flow)  , intent(in)    :: fl_src
    type(t_domain), intent(inout) :: dm_tgt
    type(t_flow)  , intent(inout) :: fl_tgt
    !
    integer  :: imode_x, imode_z, i, k
    real(WP) :: Lbuf_x, Lbuf_z
    real(WP) :: xc_src(dm_src%nc(1)), zc_src(dm_src%nc(3))
    real(WP) :: xp_src(dm_src%np(1)), zp_src(dm_src%np(3))
    !
    if (abs(dm_src%lyt - dm_tgt%lyt) > 1.0e-10_wp) then
      call print_error_msg("build_up_interp_target_field_flow: cross-section mismatch in yt")
    end if
    if (abs(dm_src%lyb - dm_tgt%lyb) > 1.0e-10_wp) then
      call print_error_msg("build_up_interp_target_field_flow: cross-section mismatch in yb")
    end if
    !-----------------------------------------
    ! extension controls
    !-----------------------------------------
    call setup_extension_mapping(dm_src%lxx, dm_tgt%lxx, dm_tgt%h(1), imode_x, Lbuf_x)
    call setup_extension_mapping(dm_src%lzz, dm_tgt%lzz, dm_tgt%h(3), imode_z, Lbuf_z)
    !-----------------------------------------
    ! source coordinates
    !-----------------------------------------
    xc_src = dm_src%h(1) * ([(real(i-1,WP) + HALF, i=1,dm_src%nc(1))])
    xp_src = dm_src%h(1) * ([(real(i-1,WP)       , i=1,dm_src%np(1))])

    zc_src = dm_src%h(3) * ([(real(k-1,WP) + HALF, k=1,dm_src%nc(3))])
    zp_src = dm_src%h(3) * ([(real(k-1,WP)       , k=1,dm_src%np(3))])

    ! qx : x-face, y-center, z-center
    call interp_field_3d_generic(dm_src, dm_tgt,                    &
        xp_src, dm_src%yc, zc_src,                                  &
        fl_src%qx, fl_tgt%qx, dm_tgt%dpcc,                          &
        XLOC_FACE, YLOC_CELL, ZLOC_CELL,                            &
        imode_x, Lbuf_x, imode_z, Lbuf_z)

    ! qy : x-center, y-face, z-center
    call interp_field_3d_generic(dm_src, dm_tgt,                    &
        xc_src, dm_src%yp, zc_src,                                  &
        fl_src%qy, fl_tgt%qy, dm_tgt%dcpc,                          &
        XLOC_CELL, YLOC_FACE, ZLOC_CELL,                            &
        imode_x, Lbuf_x, imode_z, Lbuf_z)

    ! qz : x-center, y-center, z-face
    call interp_field_3d_generic(dm_src, dm_tgt,                    &
        xc_src, dm_src%yc, zp_src,                                  &
        fl_src%qz, fl_tgt%qz, dm_tgt%dccp,                          &
        XLOC_CELL, YLOC_CELL, ZLOC_FACE,                            &
        imode_x, Lbuf_x, imode_z, Lbuf_z)

    ! pressure : x-center, y-center, z-center
    call interp_field_3d_generic(dm_src, dm_tgt,                     &
        xc_src, dm_src%yc, zc_src,                                  &
        fl_src%pres, fl_tgt%pres, dm_tgt%dccc,                      &
        XLOC_CELL, YLOC_CELL, ZLOC_CELL,                            &
        imode_x, Lbuf_x, imode_z, Lbuf_z)
    return
  end subroutine build_up_interp_target_field_flow
!==========================================================================================================
  subroutine build_up_interp_target_field_thermo(tm_src, dm_src, tm_tgt, dm_tgt)
    use parameters_constant_mod
    use print_msg_mod
    use udf_type_mod
    implicit none
    !
    type(t_domain), intent(in)    :: dm_src
    type(t_thermo), intent(in)    :: tm_src
    type(t_domain), intent(inout) :: dm_tgt
    type(t_thermo), intent(inout) :: tm_tgt
    !
    integer  :: imode_x, imode_z, i, k
    real(WP) :: Lbuf_x, Lbuf_z
    real(WP) :: xc_src(dm_src%nc(1)), zc_src(dm_src%nc(3))
    real(WP) :: xp_src(dm_src%np(1)), zp_src(dm_src%np(3))
    !
    if (abs(dm_src%lyt - dm_tgt%lyt) > 1.0e-10_wp) then
      call print_error_msg("build_up_interp_target_field_flow: cross-section mismatch in yt")
    end if
    if (abs(dm_src%lyb - dm_tgt%lyb) > 1.0e-10_wp) then
      call print_error_msg("build_up_interp_target_field_flow: cross-section mismatch in yb")
    end if
    !-----------------------------------------
    ! extension controls
    !-----------------------------------------
    call setup_extension_mapping(dm_src%lxx, dm_tgt%lxx, dm_tgt%h(1), imode_x, Lbuf_x)
    call setup_extension_mapping(dm_src%lzz, dm_tgt%lzz, dm_tgt%h(3), imode_z, Lbuf_z)
    !-----------------------------------------
    ! source coordinates
    !-----------------------------------------
    xc_src = dm_src%h(1) * ([(real(i-1,WP) + HALF, i=1,dm_src%nc(1))])
    xp_src = dm_src%h(1) * ([(real(i-1,WP)       , i=1,dm_src%np(1))])

    zc_src = dm_src%h(3) * ([(real(k-1,WP) + HALF, k=1,dm_src%nc(3))])
    zp_src = dm_src%h(3) * ([(real(k-1,WP)       , k=1,dm_src%np(3))])

    ! rhoh : x-center, y-center, z-center
    call interp_field_3d_generic(dm_src, dm_tgt,                     &
        xc_src, dm_src%yc, zc_src,                                  &
        tm_src%rhoh, tm_tgt%rhoh, dm_tgt%dccc,                      &
        XLOC_CELL, YLOC_CELL, ZLOC_CELL,                            &
        imode_x, Lbuf_x, imode_z, Lbuf_z)

    ! tTemp : x-center, y-center, z-center
    call interp_field_3d_generic(dm_src, dm_tgt,                     &
        xc_src, dm_src%yc, zc_src,                                  &
        tm_src%tTemp, tm_tgt%tTemp, dm_tgt%dccc,                      &
        XLOC_CELL, YLOC_CELL, ZLOC_CELL,                            &
        imode_x, Lbuf_x, imode_z, Lbuf_z)
    return
  end subroutine build_up_interp_target_field_thermo
!==========================================================================================================
  subroutine interp_field_3d_generic(dm_src, dm_tgt, xsrc, ysrc, zsrc, fsrc, ftgt, dtmp, &
                                    xloc_tgt, yloc_tgt, zloc_tgt,                           &
                                    extend_mode_x, extend_length_x,                         &
                                    extend_mode_z, extend_length_z)
    use parameters_constant_mod
    use udf_type_mod
    implicit none
    !
    type(t_domain)   , intent(in)    :: dm_src
    type(t_domain)   , intent(in)    :: dm_tgt
    type(DECOMP_INFO), intent(in)    :: dtmp
    real(WP)         , intent(in)    :: xsrc(:), ysrc(:), zsrc(:)
    real(WP)         , intent(in)    :: fsrc(:, :, :)
    real(WP)         , intent(inout) :: ftgt(:, :, :)
    integer          , intent(in)    :: xloc_tgt, yloc_tgt, zloc_tgt
    integer          , intent(in)    :: extend_mode_x, extend_mode_z
    real(WP)         , intent(in)    :: extend_length_x, extend_length_z
    !
    integer :: i, j, k, ii, jj, kk
    real(WP) :: x_target, y_target, z_target
    real(WP) :: x_tgt_eff, z_tgt_eff, var_target

    do k = 1, dtmp%xsz(3)
      kk = dtmp%xst(3) + k - 1
      z_target = get_coord_from_loc(3, kk, dm_tgt, zloc_tgt)
      z_tgt_eff = map_coord_to_src_bounds(z_target, zsrc(1), zsrc(size(zsrc)), &
                                          extend_length_z, extend_mode_z)

      do j = 1, dtmp%xsz(2)
        jj = dtmp%xst(2) + j - 1
        y_target = get_coord_from_loc(2, jj, dm_tgt, yloc_tgt)

        do i = 1, dtmp%xsz(1)
          ii = dtmp%xst(1) + i - 1
          x_target = get_coord_from_loc(1, ii, dm_tgt, xloc_tgt)

          x_tgt_eff = map_coord_to_src_bounds(x_target, xsrc(1), xsrc(size(xsrc)), &
                                              extend_length_x, extend_mode_x)

          call trilinear_interp_point(x_tgt_eff, y_target, z_tgt_eff, &
                                      xsrc, ysrc, zsrc, fsrc, var_target)

          ftgt(i, j, k) = var_target
        end do
      end do
    end do

  end subroutine interp_field_3d_generic
!==========================================================================================================
  pure function get_coord_from_loc(dir, idx, dm, loc_type) result(coord_val)
    use parameters_constant_mod
    use udf_type_mod
    implicit none
    !
    integer       , intent(in) :: dir
    integer       , intent(in) :: idx
    type(t_domain), intent(in) :: dm
    integer       , intent(in) :: loc_type
    real(WP)                  :: coord_val
    !
    select case (dir)
    !
    case (1)   ! x
      select case (loc_type)
      case (XLOC_CELL)
        coord_val = dm%h(1) * (real(idx - 1, WP) + HALF)
      case (XLOC_FACE)
        coord_val = dm%h(1) *  real(idx - 1, WP)
      end select
    !
    case (2)   ! y
      select case (loc_type)
      case (YLOC_CELL)
        coord_val = dm%yc(idx)
      case (YLOC_FACE)
        coord_val = dm%yp(idx)
      end select
    !
    case (3)   ! z
      select case (loc_type)
      case (ZLOC_CELL)
        coord_val = dm%h(3) * (real(idx - 1, WP) + HALF)
      case (ZLOC_FACE)
        coord_val = dm%h(3) *  real(idx - 1, WP)
      end select
    !
    end select
    return
  end function get_coord_from_loc
!==========================================================================================================
  pure function map_coord_to_src_bounds(x_tgt, x_min, x_max, Lbuf, mode) result(x_tgt_eff)
    use parameters_constant_mod
    implicit none

    real(WP), intent(in) :: x_tgt, x_min, x_max, Lbuf
    integer , intent(in) :: mode
    real(WP)             :: x_tgt_eff
    real(WP)             :: dx, Lloc, Luse, epsx

    Lloc = x_max - x_min
    epsx = TEN * epsilon(ONE) * max(ONE, abs(x_max))

    select case (mode)

    case (1)
      ! clamp to last valid source position
      x_tgt_eff = min(max(x_tgt, x_min), x_max - epsx)

    case (2)
      ! repeat last chunk
      if (x_tgt <= x_max) then
        x_tgt_eff = min(max(x_tgt, x_min), x_max - epsx)
      else
        if (Lbuf <= ZERO) then
          x_tgt_eff = x_max - epsx
        else
          Luse = min(Lbuf, Lloc)
          dx   = modulo(x_tgt - x_max, Luse)
          x_tgt_eff = x_max - Luse + dx
          x_tgt_eff = min(max(x_tgt_eff, x_min), x_max - epsx)
        end if
      end if

    case default
      x_tgt_eff = min(max(x_tgt, x_min), x_max - epsx)

    end select

  end function map_coord_to_src_bounds
!==========================================================================================================
  subroutine output_interp_target_field(dm_src, fl_src, tm_src)
    use domain_decomposition_mod
    use geometry_mod
    use input_general_mod
    use io_files_mod
    use io_restart_mod
    use parameters_constant_mod
    use print_msg_mod
    use udf_type_mod
   !use visualisation_field_mod
    implicit none 
    type(t_domain), intent(in) :: dm_src
    type(t_flow)  , intent(in) :: fl_src
    type(t_thermo), intent(in), optional :: tm_src

    if(.not.file_exists(trim(input_tgt))) then
      call Print_warning_msg('No field interpolation is carried out.')
      return
    end if
    
    if(nproc > 1) call Print_error_msg('Field interpolation and io are in serial mode only.')
    ! to do : add parallel io and interpolation if needed in the future
    ! geo/domain
    call Read_input_parameters_tgt(domain_tgt, input_tgt)
    domain_tgt%is_periodic(:) = dm_src%is_periodic(:)
    domain_tgt%ibcx_qx = dm_src%ibcx_qx
    domain_tgt%ibcy_qy = dm_src%ibcy_qy
    domain_tgt%ibcz_qz = dm_src%ibcz_qz
    domain_tgt%is_thermo = dm_src%is_thermo
    call Buildup_geometry_mesh_info(domain_tgt)
    call initialise_domain_decomposition(domain_tgt)
    ! allocate variables
    call alloc_x(flow_tgt%qx,   domain_tgt%dpcc) ; flow_tgt%qx = ZERO
    call alloc_x(flow_tgt%qy,   domain_tgt%dcpc) ; flow_tgt%qy = ZERO
    call alloc_x(flow_tgt%qz,   domain_tgt%dccp) ; flow_tgt%qz = ZERO
    call alloc_x(flow_tgt%pres, domain_tgt%dccc) ; flow_tgt%pres = ZERO
    ! interpolation from src to target
    call build_up_interp_target_field_flow(fl_src, dm_src, flow_tgt, domain_tgt)
    ! write out
    call write_instantaneous_flow(flow_tgt, domain_tgt)
    !call write_visu_flow(flow_tgt, domain_tgt)
    if(domain_tgt%is_thermo) then
      call alloc_x(thermo_tgt%rhoh,  domain_tgt%dccc) ; thermo_tgt%rhoh = ZERO
      call alloc_x(thermo_tgt%tTemp, domain_tgt%dccc) ; thermo_tgt%tTemp = ZERO
      call build_up_interp_target_field_thermo(tm_src, dm_src, thermo_tgt, domain_tgt)
      call write_instantaneous_thermo(thermo_tgt, domain_tgt)
      !call write_visu_thermo(thermo_tgt, flow_tgt, domain_tgt)
    end if 

    call Print_debug_mid_msg("Fields interpolation is completed successfully.")

    return
  end subroutine
end module 
